Fancy Mocha Pigeon

medium

# The condition check of function `DrawManager.sol#canStartDraw` is wrong.

## Summary
The condition check of function `DrawManager.sol#canStartDraw` is different from the condition of function `DrawManager.sol#startDraw`.

## Vulnerability Detail
The function `DrawManager.sol#canStartDraw` is :
```solidity
  function canStartDraw() public view returns (bool) {
    uint24 drawId = prizePool.getDrawIdToAward();
279:uint48 drawClosesAt = prizePool.drawClosesAt(drawId);
    StartDrawAuction memory lastStartDrawAuction = getLastStartDrawAuction();
    return (
      (
        // if we're on a new draw
        drawId != lastStartDrawAuction.drawId ||
        // OR we're on the same draw, but the request has failed and we haven't retried too many times
        (rng.isRequestFailed(lastStartDrawAuction.rngRequestId) && _startDrawAuctions.length <= maxRetries)
      ) && // we haven't started it, or we have and the request has failed
      block.timestamp >= drawClosesAt && // the draw has closed
289:  _computeElapsedTime(drawClosesAt, block.timestamp) <= auctionDuration // the draw hasn't expired
    );
  }
```

As we will see from the above function,  in `L289` there is a condition `_computeElapsedTime(drawClosesAt, block.timestamp) <= auctionDuration`. Here, `drawClosesAt = prizePool.drawClosesAt(drawId)`.
This is different from the `DrawManager.sol#startDraw` function.

The function `DrawManager.sol#startDraw` is :
```solidity
  function startDraw(address _rewardRecipient, uint32 _rngRequestId) external returns (uint24) {

    if (_rewardRecipient == address(0)) revert RewardRecipientIsZero();
    uint24 drawId = prizePool.getDrawIdToAward(); 
223:uint48 closesAt = prizePool.drawClosesAt(drawId);
    if (closesAt > block.timestamp) revert DrawHasNotClosed();
    if (rng.requestedAtBlock(_rngRequestId) != block.number) revert RngRequestNotInSameBlock();
    
    StartDrawAuction memory lastRequest = getLastStartDrawAuction();
    uint256 auctionOpenedAt;
    
    if (lastRequest.drawId != drawId) { // if this request is for a new draw
      // auctioned opened at the close of the draw
232:  auctionOpenedAt = closesAt;
      // clear out the old ones
      while (_startDrawAuctions.length > 0) {
        _startDrawAuctions.pop();
      }
    } else { // the old request is for the same draw
      if (!rng.isRequestFailed(lastRequest.rngRequestId)) { // if the request failed
        revert AlreadyStartedDraw();
      } else if (_startDrawAuctions.length > maxRetries) { // if request has failed and we have retried too many times
        revert RetryLimitReached();
      } else if (block.number == rng.requestedAtBlock(lastRequest.rngRequestId)) { // requests cannot be reused
        revert StaleRngRequest();
      } else {
        // auctioned opened at the close of the last auction
246:    auctionOpenedAt = lastRequest.closedAt;
      }
    }

250:uint48 auctionElapsedTimeSeconds = _computeElapsedTime(auctionOpenedAt, block.timestamp);
251:if (auctionElapsedTimeSeconds > auctionDuration) revert AuctionExpired();

    ...

    return drawId;
  }
```

In the above function `L250`, the value of the `auctionOpenedAt` variable, the parameter of function `_computeElapsedTime`, is equal to `prizePool.drawClosesAt(drawId)` only in the case of `lastRequest.drawId != drawId` as you can see in `L232`. And in the case of `lastRequest.drawId = drawId`, the value of the `auctionOpenedAt` variable is equal to `lastRequest.closedAt`.
So in the case of `lastRequest.drawId = drawId`, `lastRequest.closedAt > prizePool.drawClosesAt(drawId)`.
In conclusion, the condition of function `DrawManager.sol#canStartDraw` is different from the condition of function `DrawManager.sol#startDraw`.

## Impact
Even if the `DrawManager.sol#canStartDraw` function returns false, there is a case where a user can call the `DrawManager.sol#startDraw`.
When the `DrawManager.sol#canStartDraw` function returns false, the `DrawManager.sol#startDrawReward` function returns 0. However, in fact, the user can receive reward by calling `DrawManager.sol#startDraw`. This allows the user to make wrong decisions.

## Code Snippet
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-draw-manager/src/DrawManager.sol#L289

## Tool used

Manual Review

## Recommendation
The function `DrawManager.sol#canStartDraw` is modified as follows.
```solidity
  function canStartDraw() public view returns (bool) {
    uint24 drawId = prizePool.getDrawIdToAward();
    uint48 drawClosesAt = prizePool.drawClosesAt(drawId);
    StartDrawAuction memory lastStartDrawAuction = getLastStartDrawAuction();
++  uint256 auctionOpenedAt = drawId != lastStartDrawAuction.drawId ? drawClosesAt : lastStartDrawAuction.closedAt;

    return (
      (
        // if we're on a new draw
        drawId != lastStartDrawAuction.drawId ||
        // OR we're on the same draw, but the request has failed and we haven't retried too many times
        (rng.isRequestFailed(lastStartDrawAuction.rngRequestId) && _startDrawAuctions.length <= maxRetries)
      ) && // we haven't started it, or we have and the request has failed
      block.timestamp >= drawClosesAt && // the draw has closed
--    _computeElapsedTime(drawClosesAt, block.timestamp) <= auctionDuration // the draw hasn't expired
++     _computeElapsedTime(auctionOpenedAt, block.timestamp) <= auctionDuration // the draw hasn't expired 
    );
  }
```