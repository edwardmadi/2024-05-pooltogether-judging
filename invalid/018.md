Fancy Mocha Pigeon

medium

# `PrizeVault.sol#depositWithPermit` function restrict the caller to `_owner`.

## Summary
Restricting the caller to `_owner` makes the `PrizeVault.sol#depositWithPermit` function useless.

## Vulnerability Detail
`PrizeVault.sol#depositWithPermit` function is the following.
```solidity
    /// @notice Approve underlying asset with permit, deposit into the Vault and mint Vault shares to `_owner`.
521:/// @dev Can't be used to deposit on behalf of another user since `permit` does not accept a receiver parameter,
    /// meaning that anyone could reuse the signature and pass an arbitrary receiver to this function.
    /// @param _assets Amount of assets to approve and deposit
    /// @param _owner Address of the owner depositing `_assets` and signing the permit
    /// @param _deadline Timestamp after which the approval is no longer valid
    /// @param _v V part of the secp256k1 signature
    /// @param _r R part of the secp256k1 signature
    /// @param _s S part of the secp256k1 signature
    /// @return Amount of Vault shares minted to `_owner`.
    function depositWithPermit(
        uint256 _assets,
        address _owner,
        uint256 _deadline,
        uint8 _v,
        bytes32 _r,
        bytes32 _s
    ) external returns (uint256) {
538:    if (_owner != msg.sender) {
            revert PermitCallerNotOwner(msg.sender, _owner);
        }

        // Skip the permit call if the allowance has already been set to exactly what is needed. This prevents
        // griefing attacks where the signature is used by another actor to complete the permit before this
        // function is executed.
        if (_asset.allowance(_owner, address(this)) != _assets) {
            IERC20Permit(address(_asset)).permit(_owner, address(this), _assets, _deadline, _v, _r, _s);
        }

        uint256 _shares = previewDeposit(_assets);
550:    _depositAndMint(_owner, _owner, _assets, _shares);
        return _shares;
    }
```
`L521` describes that because of the `receiver` parameter, the above function can't be called by other users.
In fact, `L538` reverts the function when `_owner != msg.sender`.

But the `PrizeVault.sol#depositWithPermit` function has no `receiver` parameter at all.
As can be seen, `L550` passes the `_owner` to `receiver` parameter of `_depositAndMint` function.

Therefore, there is no need of restricting the caller to `_owner`.

## Impact
The purpose of calling `PrizeVault.sol#depositWithPermit` function instead of `PrizeVault.sol#deposit` function is to save gas for `_owner`.
However, restricting the caller to `_owner` contradicts the above purpose and makes the function useless at all.
That is, in the current implementation, it is more advantageous to call the `PrizeVault.sol#deposit` function directly than `PrizeVault.sol#depositWithPermit` function.

## Code Snippet
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-vault/src/PrizeVault.sol#L538

## Tool used

Manual Review

## Recommendation
Modify `PrizeVault.sol#depositWithPermit` function as follows.
```solidity
    /// @notice Approve underlying asset with permit, deposit into the Vault and mint Vault shares to `_owner`.
--  /// @dev Can't be used to deposit on behalf of another user since `permit` does not accept a receiver parameter,
--  /// meaning that anyone could reuse the signature and pass an arbitrary receiver to this function.
    /// @param _assets Amount of assets to approve and deposit
    /// @param _owner Address of the owner depositing `_assets` and signing the permit
    /// @param _deadline Timestamp after which the approval is no longer valid
    /// @param _v V part of the secp256k1 signature
    /// @param _r R part of the secp256k1 signature
    /// @param _s S part of the secp256k1 signature
    /// @return Amount of Vault shares minted to `_owner`.
    function depositWithPermit(
        uint256 _assets,
        address _owner,
        uint256 _deadline,
        uint8 _v,
        bytes32 _r,
        bytes32 _s
    ) external returns (uint256) {
--      if (_owner != msg.sender) {
--          revert PermitCallerNotOwner(msg.sender, _owner);
--      }
--
        // Skip the permit call if the allowance has already been set to exactly what is needed. This prevents
        // griefing attacks where the signature is used by another actor to complete the permit before this
        // function is executed.
        if (_asset.allowance(_owner, address(this)) != _assets) {
            IERC20Permit(address(_asset)).permit(_owner, address(this), _assets, _deadline, _v, _r, _s);
        }

        uint256 _shares = previewDeposit(_assets);
        _depositAndMint(_owner, _owner, _assets, _shares);
        return _shares;
    }
```