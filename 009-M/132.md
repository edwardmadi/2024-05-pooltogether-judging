Rhythmic Chili Swallow

medium

# Vault's `maxDeposit()` returns a positive value while `deposit()` reverts violating EIP-4626

## Summary
`PrizeVault:maxDeposit()` returns maximum amount of the underlying asset that can be deposited into the Vault and according to EIP4626,  if deposits are entirely disabled (even temporarily) it MUST return 0. Which is not the case here as deposit will revert but maxDeposit() will continue to return some positive value.

## Vulnerability Detail
```solidity
    function maxDeposit(address /* receiver */) public view returns (uint256) {
        uint256 _totalDebt = totalDebt();
        (bool _success, uint256 _totalAssets) = _tryGetTotalPreciseAssets();
        if (!_success || _totalAssets < _totalDebt) return 0;

        uint256 _latentBalance = _asset.balanceOf(address(this));
        uint256 _maxYieldVaultDeposit = yieldVault.maxDeposit(address(this));
        if (_latentBalance >= _maxYieldVaultDeposit) {
            return 0;
        } else {
            // the vault will never mint more than 1 share per asset, so no need to convert mint limit to assets
            uint256 _depositLimit = _mintLimit(_totalDebt);
            uint256 _maxDeposit;
            unchecked {
                _maxDeposit = _maxYieldVaultDeposit - _latentBalance;
            }
            return _depositLimit < _maxDeposit ? _depositLimit : _maxDeposit;
        }
    }
```
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-vault/src/PrizeVault.sol#L383C1-L401C6

Suppose protocol is not in lossyState i.e` _totalAssets = _totalDebt` so protocol  will return a positive value based on yieldVault limit and twablimit

```solidity
    function deposit(uint256 _assets, address _receiver) external returns (uint256) {
        uint256 _shares = previewDeposit(_assets);
        _depositAndMint(msg.sender, _receiver, _assets, _shares);
        return _shares;
    }

 function _depositAndMint(address _caller, address _receiver, uint256 _assets, uint256 _shares) internal {
        if (_shares == 0) revert MintZeroShares();
        if (_assets == 0) revert DepositZeroAssets();

        // If _asset is ERC777, `transferFrom` can trigger a reentrancy BEFORE the transfer happens through the
        // `tokensToSend` hook. On the other hand, the `tokenReceived` hook that is triggered after the transfer
        // calls the vault which is assumed to not be malicious.
        //
        // Conclusion: we need to do the transfer before we mint so that any reentrancy would happen before the
        // assets are transferred and before the shares are minted, which is a valid state.

        _asset.safeTransferFrom(
            _caller,
            address(this),
            _assets
        );

        // Previously accumulated dust is swept into the yield vault along with the deposit.
        uint256 _assetsWithDust = _asset.balanceOf(address(this));
        _asset.forceApprove(address(yieldVault), _assetsWithDust);

        // The shares are calculated and then minted directly to mitigate rounding error loss.
        uint256 _yieldVaultShares = yieldVault.previewDeposit(_assetsWithDust);
        yieldVault.mint(_yieldVaultShares, address(this));

        // Enforce the mint limit and protect against lossy deposits.
        uint256 _totalDebtBeforeMint = totalDebt();
        _enforceMintLimit(_totalDebtBeforeMint, _shares);
        if (totalPreciseAssets() < _totalDebtBeforeMint + _shares) {
            revert LossyDeposit(totalPreciseAssets(), _totalDebtBeforeMint + _shares);
        }

        _mint(_receiver, _shares);

        emit Deposit(_caller, _receiver, _assets, _shares);
    }
```
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-vault/src/PrizeVault.sol#L991
A user decides to deposit a value which is less than `maxDeposit()`, All the execution will happen smoothly in deposit function but at the end `_depositAndMint()` ensure that deposit doesn't put vault in lossy state which is root issue. 
```solidity
  if (totalPreciseAssets() < _totalDebtBeforeMint + _shares) {
            revert LossyDeposit(totalPreciseAssets(), _totalDebtBeforeMint + _shares);
        }
```
if deposit putting vault in lossy state it will revert which can be case due to rounding errro
Quoting sponsor
> Rounding errors can occur on the deposit to the yield vault. This can cause a loss of assets, so we check at the end of the deposit flow to ensure any loss was managed by the yield buffer

In this state deposit is temporary blocked but maxDeposit() continue returning a positive value which is violation of EIP4626
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-vault/src/PrizeVault.sol#L991
## Tool used

Manual Review

## Recommendation
Ensure that maxDeposit returns zero if deposit reverts 