Melted Coconut Unicorn

medium

# If a successful `startDrawAuction` has expired, `finishDraw` will always revert even if the draw has not finalized.

## Summary
If a successful `startDrawAuction` has expired, `finishDraw` will always revert even if the draw has not finalized, causing the draw to be unable to be awarded.

## Vulnerability Detail
After the success of the `startDrawAuction`, `finishDraw` needs to be called within an auction duration; otherwise, the `startDrawAuction` will expire and `finishDraw` will revert (L329). If `startDrawAuction` has expired and there is no new `startDrawAuction`, then all subsequent `finishDraw` attempts will revert, even if the draw has not finalized, making the draw cannot be awarded.
```solidity
314:  function finishDraw(address _rewardRecipient) external returns (uint24) {
315:    if (_rewardRecipient == address(0)) {
316:      revert RewardRecipientIsZero();
317:    }
318:
319:@>  StartDrawAuction memory startDrawAuction = getLastStartDrawAuction();
320:    
321:    if (startDrawAuction.drawId != prizePool.getDrawIdToAward()) {
322:      revert DrawHasFinalized();
323:    }
324:
325:    if (!rng.isRequestComplete(startDrawAuction.rngRequestId)) {
326:      revert RngRequestNotComplete();
327:    }
328:
329:@>  if (_isAuctionExpired(startDrawAuction.closedAt)) {
330:      revert AuctionExpired();
331:    }
```
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-draw-manager/src/DrawManager.sol#L314-L331

The question is, if the last successful `startDrawAuction` has expired, will there be a new `startDrawAuction` afterwards? Unfortunately no. As shown in function `startDraw`, if the last `startDrawAuction` (i.e. `lastRequest` at L227) has already succeeded, it will prevent subsequent `startDraw` (revert at L239), and there's no new `startDrawAuction` will be pushed into `_startDrawAuctions`. Therefore, subsequent `finishDraw` will revert and the draw will not be awarded.
```solidity
219:  function startDraw(address _rewardRecipient, uint32 _rngRequestId) external returns (uint24) {
220:
221:    if (_rewardRecipient == address(0)) revert RewardRecipientIsZero();
222:    uint24 drawId = prizePool.getDrawIdToAward(); 
223:    uint48 closesAt = prizePool.drawClosesAt(drawId);
224:    if (closesAt > block.timestamp) revert DrawHasNotClosed();
225:    if (rng.requestedAtBlock(_rngRequestId) != block.number) revert RngRequestNotInSameBlock();
226:    
227:@>  StartDrawAuction memory lastRequest = getLastStartDrawAuction();
228:    uint256 auctionOpenedAt;
229:    
230:    if (lastRequest.drawId != drawId) { // if this request is for a new draw
231:      // auctioned opened at the close of the draw
232:      auctionOpenedAt = closesAt;
233:      // clear out the old ones
234:      while (_startDrawAuctions.length > 0) {
235:        _startDrawAuctions.pop();
236:      }
237:    } else { // the old request is for the same draw
238:@>    if (!rng.isRequestFailed(lastRequest.rngRequestId)) { // if the request failed
239:@>      revert AlreadyStartedDraw();
240:      } else if (_startDrawAuctions.length > maxRetries) { // if request has failed and we have retried too many times
241:        revert RetryLimitReached();
242:      } else if (block.number == rng.requestedAtBlock(lastRequest.rngRequestId)) { // requests cannot be reused
243:        revert StaleRngRequest();
244:      } else {
245:        // auctioned opened at the close of the last auction
246:        auctionOpenedAt = lastRequest.closedAt;
247:      }
248:    }
249:
250:    uint48 auctionElapsedTimeSeconds = _computeElapsedTime(auctionOpenedAt, block.timestamp);
251:    if (auctionElapsedTimeSeconds > auctionDuration) revert AuctionExpired();
252:
253:@>  _startDrawAuctions.push(StartDrawAuction({
254:      recipient: _rewardRecipient,
255:      closedAt: uint40(block.timestamp),
256:      drawId: drawId,
257:      rngRequestId: _rngRequestId
258:    }));
```
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-draw-manager/src/DrawManager.sol#L219-L258

POC:
1. We assume auction duration is 6 hours, and draw period is 24 hours, current draw id is 1, the draw closed at hour 0.
2. At hour 3, a `startDraw` succeeds, and `startDrawAuction.closedAt = hour 3`. Then subsequent `startDraw`s will revert with `AlreadyStartedDraw`.
3. At hour 10, `finishDraw` is called, but revert, as the last `startDrawAuction` has expired (10-3=7 hours > auction duration).
4. At this time, there are 24-10=14 hours left before the draw is finalized. But subsequent `finishDraw`s will revert with `AuctionExpired` and will no be able to finalize the draw.

## Impact
If a successful `startDrawAuction` has expired, the draw cannot be successfully awarded even if it has not been finalized.

## Code Snippet
https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-draw-manager/src/DrawManager.sol#L314-L331

https://github.com/sherlock-audit/2024-05-pooltogether/blob/main/pt-v5-draw-manager/src/DrawManager.sol#L219-L258

## Tool used

Manual Review

## Recommendation
In `startDraw`, revert with `AlreadyStartedDraw` only when the last auction is neither failed nor expired.
```solidity
// Function: startDraw, L238
-    if (!rng.isRequestFailed(lastRequest.rngRequestId)) { // if the request failed
+    if (!rng.isRequestFailed(lastRequest.rngRequestId) && !_isAuctionExpired(startDrawAuction.closedAt))
```